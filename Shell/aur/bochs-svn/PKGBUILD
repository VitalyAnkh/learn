# Maintainer: Vitaly Ankh <vitalyankh@gmail.com>
# Contributor: Peter Vanusanik <admin@en-circle.com>
# stolen from bochs PKGBUILD in normal repos, but modified

pkgname=bochs-svn
pkgver=VERSION
pkgrel=1
pkgdesc="A portable x86 PC emulation software package with gdbstub"
arch=('i686' 'x86_64')
url="http://bochs.sourceforge.net/"
license=('LGPL')
depends=('gcc-libs' 'libxrandr' 'libxpm' 'gtk2' 'bochs')
source=("bochs-svn::svn+svn://svn.code.sf.net/p/bochs/code/trunk")
md5sums=('SKIP')

cd $srcdir
prepare() {
    cd "$srcdir/bochs-svn/bochs"
    # 4.X kernel is basically 3.20
    sed -i 's/2\.6\*|3\.\*)/2.6*|3.*|4.*)/' configure*
}
pkgver() {
    cd "$srcdir/bochs-svn/bochs"
    printf "r%s" "$(svnversion | tr -d 'A-z')"
}

build() {
    cd "$srcdir/bochs-svn/bochs"

    ./configure \
        --prefix=/usr \
        --without-wx \
        --with-x11 \
        --with-x \
        --with-term \
        --disable-docbook \
        --enable-cpu-level=6 \
        --enable-fpu \
        --enable-3dnow \
        --enable-disasm \
        --enable-x86-64 \
        --enable-avx \
        --enable-long-phy-address \
        --enable-disasm \
        --enable-usb \
        --enable-gdb-stub

    sed -i 's/^LIBS = /LIBS = -lpthread/g' Makefile
    make -j 1
}

package() {
    cd "$srcdir/bochs-svn/bochs"
    make DESTDIR="$pkgdir" install
    install -Dm644 .bochsrc "$pkgdir/etc/bochsrc-sample.txt"

    cd "$pkgdir/usr/bin/"
    mv bochs bochs-gdb-a20
    rm bximage
    cd "$pkgdir/usr/"
    rm -rfv share
    cd "$pkgdir"
    rm -rfv etc
}
